{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "social-lending",
   "metadata": {},
   "source": [
    "# データ解析\n",
    "\n",
    "研究室の都合上、SASを使用してデータ分析を行った。\n",
    "\n",
    "## ファイル読み込み"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "naked-emergency",
   "metadata": {},
   "outputs": [],
   "source": [
    "OPTION NOSOURCE;\n",
    "  \n",
    "  %macro obscnt(data=,mvar=);\n",
    "    %global &mvar;\n",
    "    data _null_;\n",
    "      set &data nobs=cnt;\n",
    "      call symput(\"&mvar\",left(put(cnt,8.)));\n",
    "      stop;\n",
    "    run;\n",
    "  %mend;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "unexpected-appreciation",
   "metadata": {},
   "source": [
    "## 一般化推定方程式(gee)と一般化線形混合効果モデル(glmm)を用いた分析"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "metropolitan-testament",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "%GLOBAL counter;\n",
    "%let counter=0;\n",
    "\n",
    "%macro doexe(filepath);\n",
    "  %let counter=%eval(&counter+1);\n",
    "  title &counter;\n",
    "  filename FL &filepath;\n",
    "  \n",
    "  data start_prj_multi&counter;\n",
    "    infile FL eov=eov;\n",
    "    eov = 0 ;\n",
    "    input @;\n",
    "    if _N_ ^= 1 and eov ^= 1 then do;\n",
    "      input outcome patient_id x1 w1 x2 w2;\n",
    "      output;\n",
    "    end;;\n",
    "  run;\n",
    "  \n",
    "  %obscnt(data=start_prj_multi&counter,mvar=obs);\n",
    "\n",
    "  data prj_multi&counter;\n",
    "    set start_prj_multi&counter;\n",
    "    if _N_ <= &obs * 0.8 then _Imputation_ = 4 ; else _Imputation_ = 5;\n",
    "    if _N_ <= &obs * 0.6 then _Imputation_ = 3 ;\n",
    "    if _N_ <= &obs * 0.4 then _Imputation_ = 2 ;\n",
    "    if _N_ <= &obs * 0.2 then _Imputation_ = 1 ;\n",
    "  run;\n",
    "  \n",
    "  data trans&counter;\n",
    "    set prj_multi&counter;\n",
    "    p1 = input(x1,8.);\n",
    "    p2 = input(x2,8.);\n",
    "    drop x1 x2;\n",
    "  run;\n",
    "  \n",
    "  proc sort data=trans&counter;\n",
    "    by _Imputation_ patient_id;\n",
    "  run;\n",
    "  \n",
    "  data PRJ_MULTI_GEE&counter;\n",
    "    set trans&counter;\n",
    "    by _Imputation_ patient_id;\n",
    "    if first.patient_id then SEQ = 0;\n",
    "    SEQ+1;\n",
    "  run;\n",
    "  \n",
    "  ods select none;\n",
    "  ods output GEEFitCriteria = fitgee&counter;\n",
    "  ods output GEEEmpPEst = pestgee&counter;\n",
    "  proc genmod data = PRJ_MULTI_GEE&counter descending;\n",
    "    BY _Imputation_;\n",
    "    CLASS outcome patient_id SEQ;\n",
    "    MODEL outcome(event='1') = p1 p2 / dist = binomial link = logit;\n",
    "    REPEATED SUBJECT=patient_id / WITHINSUBJECT=SEQ TYPE=CS;\n",
    "  run;\n",
    "  ods select all;\n",
    "\n",
    "  data fitgee_processed&counter;\n",
    "    set fitgee&counter;\n",
    "    where Criterion = 'QIC';\n",
    "    fileid = &counter;\n",
    "  run;\n",
    "   \n",
    "  data pest_geeforint&counter;\n",
    "    set pestgee&counter;\n",
    "    keep _Imputation_ Parm Estimate Stderr;\n",
    "  run;\n",
    " \n",
    "  ods select none;\n",
    "  ods output ParameterEstimates = mianalyzed_gee&counter;\n",
    "  proc mianalyze parms=pest_geeforint&counter;\n",
    "    modeleffects Intercept p1 p2;\n",
    "  run;\n",
    "  ods select all;\n",
    "  \n",
    "  data mianalyzed_gee&counter;\n",
    "    set mianalyzed_gee&counter;\n",
    "    odds_ratio = exp(estimate);\n",
    "    lcl=exp(estimate-probit(0.975)*stderr);\n",
    "    ucl=exp(estimate+probit(0.975)*stderr);\n",
    "    fileid = &counter;\n",
    "  run;\n",
    "  \n",
    "  ods select none;\n",
    "  ods output FitStatistics=fit_glmm&counter;\n",
    "  ods output ParameterEstimates=pest_glmm&counter;\n",
    "  PROC glimmix data =trans&counter method=quad INFOCRIT=Q;\n",
    "    BY _Imputation_;\n",
    "    CLASS outcome patient_id;\n",
    "    MODEL outcome(event='1') = p1 p2 / dist = binary oddsratio link=logit s covb;\n",
    "    RANDOM intercept / subject = patient_id;\n",
    "  RUN;\n",
    "  ods select all;\n",
    "  \n",
    "  /*適合度*/\n",
    "  data fit_glmm_processed&counter;\n",
    "    set fit_glmm&counter;\n",
    "    where substr(Descr,1,4) = 'AIC ';\n",
    "    fileid = &counter;\n",
    "  run;\n",
    "   \n",
    "  ods select none;\n",
    "  ods output ParameterEstimates = mianalyzed_glmm&counter;\n",
    "  proc mianalyze parms=pest_glmm&counter;\n",
    "    modeleffects Intercept p1 p2;\n",
    "  run;\n",
    "  ods select all;\n",
    "\n",
    "  data mianalyzed_glmm&counter;\n",
    "    set mianalyzed_glmm&counter;\n",
    "    odds_ratio = exp(estimate);\n",
    "    lcl=exp(estimate-probit(0.975)*stderr);\n",
    "    ucl=exp(estimate+probit(0.975)*stderr);\n",
    "    fileid = &counter;\n",
    "  run;\n",
    "\n",
    "%mend;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "laden-mountain",
   "metadata": {},
   "source": [
    "## miceで作成したデータの統合・分析\n",
    "Miceで補完したデータを統合分析を行うため、$\\ast$を指定し、上の関数で分析を行った．"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dedicated-carol",
   "metadata": {},
   "outputs": [],
   "source": [
    "/* 5ファイルを統合する */\n",
    "%doexe('<folder>\\22_Model7mar_imp_001__IMPDATA*.dat');\n",
    "/* 以下続く */"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "forbidden-player",
   "metadata": {},
   "source": [
    "## 結果の出力"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "oriental-economics",
   "metadata": {},
   "outputs": [],
   "source": [
    "DATA results_gee;\n",
    "  set mianalyzed_gee:;\n",
    "run;\n",
    "\n",
    "proc sort data=results_gee;\n",
    "  by fileid;\n",
    "run;\n",
    "\n",
    "PROC EXPORT DATA=results_gee\n",
    "            OUTFILE= \"<folder>\\gee.csv\"\n",
    "            DBMS=CSV REPLACE;\n",
    "RUN;\n",
    "\n",
    "DATA results_glmm;\n",
    "  set mianalyzed_glmm:;\n",
    "run;\n",
    "\n",
    "proc sort data=results_glmm;\n",
    "  by fileid;\n",
    "run;\n",
    "\n",
    "PROC EXPORT DATA=results_glmm\n",
    "            OUTFILE= \"<folder>\\glmm.csv\"\n",
    "            DBMS=CSV REPLACE;\n",
    "RUN;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
